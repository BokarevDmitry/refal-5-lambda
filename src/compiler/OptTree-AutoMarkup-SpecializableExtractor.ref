$INCLUDE "LibraryEx";

*$FROM GlobalGen
$EXTERN GlobalGen;

*$FROM GenericMatch
$EXTERN GenericMatch;

*$FROM Log
$EXTERN Log-PutLine;

/**
  <OptTree-AutoMarkup-GetSpecializableDict (e.AST)>
    == ((e.Name) e.SpecPattern)*

  e.SpecPattern ::= e.Pattern
*/

$ENTRY OptTree-AutoMarkup-GetSpecializableDict {
  e.AST
    = <Map
        {
          (Function s.ScopeClass (e.Name) Sentences /* нет предложений */)
            = /* пусто */;

          (Function s.ScopeClass (e.Name) Sentences e.Body)
            = <Map
                {
                  ((e.Pattern) e.Conditions (e.Result)) = (e.Pattern);
                }
                e.Body
              >
              : e.ExtractedPatterns
            = <ContainsDuplications (e.ExtractedPatterns)>
            : {
                True = /* пусто */;
                False = <GetSpecifiedPattern (e.Name) e.ExtractedPatterns>;
              };

          t.Other = /* пусто */;
        }
        e.AST
      >;
}

ContainsDuplications {
  (e.ExtractedPatterns)
    = <Map
        {
          (e.Patterns)
            = <Map &GetPatternVariables e.Patterns>
            : {
                e.Head (e.Variable) e._ (e.Variable) e.Tail = (True);

                e.Other = (False);
              };
        }
        e.ExtractedPatterns
      >
    : {
        e.Head (True) e.Tail = True;

        e.Other = False;
      }
}

GetPatternVariables {
  (TkVariable e.Name) = (e.Name);

  (Symbol Identifier VAR e.Name) = (e.Name);

  (Brackets e.Nested)
    = <Map
        {
          (e.SubPattern) = <GetPatternVariables (e.SubPattern)>;
        }
        e.Nested
      >;

  (ADT-Brackets (e.Name) e.Nested)
    = <Map
        {
          (e.SubPattern) = <GetPatternVariables (e.SubPattern)>;
        }
        e.Nested
      >;

  (e.Other)
    = ;
}

GetSpecifiedPattern {
  (e.Name) e.ExtractedPatterns
    = <GetIndexedGlobalGen e.ExtractedPatterns> : e.IndexedGlobalGen
    = <Map
        {
          (e.ConcretePattern)
            = <GenericMatch (e.ConcretePattern) (e.IndexedGlobalGen)>
              : e.GenericMatch
            = (e.GenericMatch);
        }
        e.ExtractedPatterns
      >
      : e.GenericMatches
    = <GetVariableMatches e.GenericMatches (e.IndexedGlobalGen)>
      : e.VariableMatchesDict
    = <GetVariableSpecType (e.VariableMatchesDict)>
    : {
        e.TypedVariables
          , e.TypedVariables : e._ (s._mode 'STAT' e.Suffix) e._
          = <MarkupVarsInPattern (e.TypedVariables) e.IndexedGlobalGen>
          : e.Pattern
          = ((e.Name) e.Pattern);

        e._ = /* нет статических переменных, пропускаем */;
      };
}

GetIndexedGlobalGen {
  e.Patterns
    = <GlobalGen e.Patterns> : e.GlobalGen
    = <FillIndexes (e.GlobalGen) 't'> : e.GlobalGen^
    = <FillIndexes (e.GlobalGen) 'e'> : e.GlobalGen^
    = <FillIndexes (e.GlobalGen) 's'> : e.GlobalGen^
    = e.GlobalGen;
}

FillIndexes {
  (e.GlobalGen) s.Type
    = <FillIndexesRec (0) (e.GlobalGen) s.Type>
      : (e._) e.IndexedTypeGlobalGens
    = e.IndexedTypeGlobalGens;
}

FillIndexesRec {
  (e.StartIndex) (e.GlobalGen) s.Type
    = <MapAccum
        {
          (e.CurrentIndex) (TkVariable s.Type)
            = TkVariable s.Type e.CurrentIndex
              : e.IndexedVariable
            = <Add e.CurrentIndex 1>
              : e.NewIndex
            = (e.NewIndex) (e.IndexedVariable);

          (e.CurrentIndex) (TkVariable e.Other)
            = (e.CurrentIndex) (TkVariable e.Other);

          (e.CurrentIndex) (Brackets e.Nested)
            = <FillIndexesRec
                (e.CurrentIndex)
                (e.Nested)
                s.Type
              >
              : (e.NextIndex) e.IndexedTypeGlobalGens
            = (e.NextIndex) (Brackets e.IndexedTypeGlobalGens);

          (e.CurrentIndex) (ADT-Brackets (e.Name) e.Nested)
            = <FillIndexesRec
                (e.CurrentIndex)
                (e.Nested)
                s.Type
              >
              : (e.NextIndex) e.IndexedTypeGlobalGens
            = (e.NextIndex) (ADT-Brackets (e.Name) e.IndexedTypeGlobalGens);

          (e.CurrentIndex) (Symbol e.Info) = (e.CurrentIndex) (Symbol e.Info);
        }
        (e.StartIndex) e.GlobalGen
      >
}

GetVariableMatchesByIndexedGlobalGen {
  TkVariable e.VariableName (e.GenericMatches)
    = <Map
        {
          (Clear e.Dict)
            = <GetMatchesByVariableName
                (e.VariableName)
                (e.Dict)
              >;
        }
        e.GenericMatches
      >
      : e.CurrentVariableRes
    = <GetVariablesTypes e.CurrentVariableRes>
      : e.CurrentVariableTypeRes
    = (e.VariableName ':' e.CurrentVariableTypeRes);

  Brackets e.Nested (e.GenericMatches)
    = <GetVariableMatchesByIndexedGlobalGen
        e.Nested
        (e.GenericMatches)
      >;

  ADT-Brackets (e.Name) e.Nested (e.GenericMatches)
    = <GetVariableMatchesByIndexedGlobalGen
        e.Nested
        (e.GenericMatches)
      >;

  (e.Nested) (e.GenericMatches)
    = <GetVariableMatchesByIndexedGlobalGen
        e.Nested
        (e.GenericMatches)
      >;

  e.Other
    = ;
}

GetVariableMatches {
  e.GenericMatches (e.IndexedGlobalGen)
    = <Map
        {
          e.IndexedGlobalGenNode
            = <GetVariableMatchesByIndexedGlobalGen
                (e.IndexedGlobalGenNode)
                (e.GenericMatches)
              >;
        }
        e.IndexedGlobalGen
      >
}

GetVariablesTypes {
  () = ();

  TkVariable 's' e._ = TkVariable 's';
  TkVariable 't' e._ = TkVariable 't';
  TkVariable 'e' e._ = TkVariable 'e';

  Symbol e.Tail = Symbol e.Tail;

  Brackets e.Nested
    = <Map &GetVariablesTypes e.Nested> : e.NestedRes
    = Brackets e.NestedRes;

  ADT-Brackets (e.Name) e.Nested
    = <Map &GetVariablesTypes e.Nested> : e.NestedRes
    = ADT-Brackets (e.Name) e.NestedRes;

  (e.Nested) e.Tail
    = (<GetVariablesTypes e.Nested>)
      <GetVariablesTypes e.Tail>;

  e.Other = e.Other;
}

IsAllEqual {
  (e.Single) = True;

  (e.Value) (e.Value) = True;

  (e.First) (e.Next) e.Tail
    , e.Next : e.First
    = <IsAllEqual (e.First) e.Tail>;

  e._ = False;
}

GetMatchesByVariableName {
  (e.Key) (e.Head (e.Value ':' (e.Key)) e.Tail)
    = (e.Value);

  (e.Key) e.Other
    = ;
}

GetVariableSpecType {
  (e.VariableMatchesDict)
    = <Map
        {
          (t.VariableType t.VariableIndex ':' e.Matches)
            = <IsAllEqual e.Matches>
              : {
                  True = (t.VariableType 'STAT' t.VariableIndex);
                  False = (t.VariableType 'dyn' t.VariableIndex);
                }
        }
        e.VariableMatchesDict
      >
}

MarkupVarsInPattern {
  (e.TypedVariables) e.Pattern
    = <Map
        {
          (TkVariable s.Mode s.Index)
            , e.TypedVariables : e._ (s.Mode e.Type s.Index) e._
            = (TkVariable s.Mode e.Type s.Index);

          (Symbol e.Info) = (Symbol e.Info);

          (Brackets e.Expr)
            = (Brackets <MarkupVarsInPattern (e.TypedVariables) e.Expr>);

          (ADT-Brackets (e.Name) e.Expr)
            = (ADT-Brackets
                (e.Name) <MarkupVarsInPattern (e.TypedVariables) e.Expr>
              );
        }
        e.Pattern
      >;
}
