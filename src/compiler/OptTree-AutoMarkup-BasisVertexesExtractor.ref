$INCLUDE "LibraryEx";

*$FROM OptTree-AutoMarkup-Common
$EXTERN OptTree-AutoMarkup-GetElemByKey;


/**
  <OptTree-AutoMarkup-ExtractBasisVertexes e.ExtractedGraph (e.RootSet)>
    = (e.BasisVertex)* (e.Graph)

  e.BasisVertex ::= e.FuncName
*/

$ENTRY OptTree-AutoMarkup-ExtractBasisVertexes {
  e.Graph (e.GraphRoots)
    = <GetBasisVertexes
        (e.GraphRoots)
        e.Graph
      >
      : (
          (e.CurrentPath)
          (e.Proccessed)
          (e.BasisVertexes)
          (e.Graph^)
        )
    = (e.BasisVertexes) (e.Graph);
}

GetBasisVertexes-GetResultByNode {
  (Func (e.Name) Children (e.Children))
  ((e.CurrentPath) (e.Proccessed) (e.BasisVertexes) (e.Graph))
    = <MapAccum
        {
          ((e.CurrentPath^) (e.Proccessed^) (e.BasisVertexes^) (e.Graph^))
          (e.ChildFunc)
            , e.BasisVertexes : e._ (e.ChildFunc) e._
            = (
                (e.CurrentPath)
                (e.Proccessed)
                (e.BasisVertexes)
                (e.Graph)
              );

          ((e.CurrentPath^) (e.Proccessed^) (e.BasisVertexes^) (e.Graph^))
          (e.Name)
            , e.CurrentPath : e._ (e.ChildFunc) e._
            = (
                (e.CurrentPath)
                (e.Proccessed)
                (e.BasisVertexes (e.Name))
                (e.Graph)
              );

          ((e.CurrentPath^) (e.Proccessed^) (e.BasisVertexes^) (e.Graph^))
          (e.ChildFunc)
            , e.CurrentPath : e._ (e.ChildFunc) e._
            = (
                (e.CurrentPath)
                (e.Proccessed)
                (e.BasisVertexes (e.ChildFunc))
                (e.Graph)
              );

          ((e.CurrentPath^) (e.Proccessed^) (e.BasisVertexes^) (e.Graph^))
          (e.ChildFunc)
            , e.Proccessed : e._ (e.ChildFunc) e._
            = (
                (e.CurrentPath)
                (e.Proccessed)
                (e.BasisVertexes)
                (e.Graph)
              );

          ((e.CurrentPath^) (e.Proccessed^) (e.BasisVertexes^) (e.Graph^))
          (e.ChildFunc)
            = e.CurrentPath (e.ChildFunc) : e.NewCurrentPath
            = <GetBasisVertexes
                ((e.ChildFunc))
                (
                  (e.NewCurrentPath)
                  (e.Proccessed)
                  (e.BasisVertexes)
                  (e.Graph)
                )
              >
              : (
                  (e.NewCurrentPath^)
                  (e.Proccessed^)
                  (e.BasisVertexes^)
                  (e.Graph^)
                )
            = (
                (e.CurrentPath)
                (e.Proccessed)
                (e.BasisVertexes)
                (e.Graph)
              );
        }
        ((e.CurrentPath) (e.Proccessed) (e.BasisVertexes) (e.Graph))
        e.Children
      >
      : ((e.CurrentPath^) (e.Proccessed^) (e.BasisVertexes^) (e.Graph^))
      = e.Proccessed (e.Name) : e.NewProccessed
      = (
          (e.CurrentPath)
          (e.NewProccessed)
          (e.BasisVertexes)
          (e.Graph)
        );

  (e.Other)
  ((e.CurrentPath) (e.Proccessed) (e.BasisVertexes) (e.Graph))
    = ((e.CurrentPath) (e.Proccessed) (e.BasisVertexes) (e.Graph));
}


GetBasisVertexes {
  (e.NextGraphNodes)
  (
    (e.CurrentPath)
    (e.Proccessed)
    (e.BasisVertexes)
    (e.Graph)
  )
    = <MapAccum
        {
          ((e.CurrentPath^)(e.Proccessed^)(e.BasisVertexes^)(e.Graph^))
          (e.FuncName)
            = <OptTree-AutoMarkup-GetElemByKey
                (e.FuncName)
                (e.Graph)
              >
              : e.Node (e.Graph^)
            = <GetBasisVertexes-GetResultByNode
                (e.Node)
                ((e.CurrentPath)(e.Proccessed)(e.BasisVertexes)(e.Graph))
              >
        }
        (
          (e.CurrentPath)
          (e.Proccessed)
          (e.BasisVertexes)
          (e.Graph)
        )
        e.NextGraphNodes
      >;

  (e.RootNodes)
  e.Graph
    = <GetBasisVertexes
        (e.RootNodes)
        (
          ()
          ()
          ()
          (e.Graph)
        )
      >;
}
