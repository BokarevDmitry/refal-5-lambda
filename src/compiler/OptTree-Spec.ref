$INCLUDE "LibraryEx";

*$FROM TreeUtils
$EXTERN ExtractVariables, ExtractVariables-Expr, NewVarName, AddSuffix;

*$FROM GenericMatch
$EXTERN GenericMatch;

*$FROM Log
$EXTERN Log-PutLine;

*$FROM DisplayName
$EXTERN DisplayName;


/**
  <OptTree-Spec-Prepare s.OptSpec e.AST> == e.AST^

  s.OptSpec ::= OptSpec | NoOpt

  В дерево добавляется узел (SpecInfo e.SpecInfo).

  e.SpecInfo ::= (e.SpecFuncNames) e.SpecInfo-Specific
  e.SpecFuncNames ::= (e.FuncName)*
  e.SpecInfo-Specific ::= t.FunctionInfo*
  t.FunctionInfo ::= ((e.Name) (e.Pattern) (e.Body) s.NextCounter t.Signature*)
  t.Signature ::= ((e.InstanceName) t.StaticVarVals*)
  t.StaticVarVals ::= (e.Expression)
  e.InstanceName ::= e.Name
*/
$ENTRY OptTree-Spec-Prepare {
  NoOpt e.AST = e.AST;

  OptSpec e.AST-B (SpecInfo e.SpecInfo) e.AST-E
    = <Prepare-Aux (e.SpecInfo) e.AST-B e.AST-E>;

  OptSpec e.AST
    = (/* no names */) /* no info */ : e.EmptyInfo
    = <Prepare-Aux (e.EmptyInfo) e.AST>;
}

Prepare-Aux {
  ((e.KnownNames) e.KnownInfo) e.AST
    = <Fetch
        ((/* no names */) /* no info */) e.AST
        <Pipe
          (&MapAccum
            {
              /* Игнорируем новые $SPEC’и для известных функций */
              ((e.Names) e.Info) (Spec t.Name e.Pattern)
                , e.KnownNames : e._B t.Name e._E
                = ((e.Names) e.Info) /* пропускаем */;

              ((e.Names) e.Info) (Spec t.Name e.Pattern)
                = ((e.Names t.Name) e.Info (t.Name (e.Pattern)));

              ((e.Names) e.Info) t.Other = ((e.Names) e.Info) t.Other;
            }
          )
          (&MapAccum
            {
              ((e.Names) e.Info-B (t.Name (e.Pattern)) e.Info-E)
              (Function s.ScopeClass t.Name Sentences e.Body)
                = ((e.Names) e.Info-B (t.Name (e.Pattern) (e.Body) 1) e.Info-E)
                  (Function s.ScopeClass t.Name Sentences e.Body);

               ((e.Names) e.Info) t.Other = ((e.Names) e.Info) t.Other;
            }
          )
        >
      >
    : ((e.NewNames) e.NewInfo) e.AST^

    = <CreateGuardFunctions e.NewInfo> : (e.GuardEnums) e.NewInfo^

    = <CreateRemainderFunctions e.NewInfo> : e.NewInfo^

    = (SpecInfo (e.KnownNames e.NewNames) e.KnownInfo e.NewInfo)
      e.AST
      e.GuardEnums

      <Map
        {
          (e.Name) = <Log-PutLine 'New $SPEC function: ' <DisplayName e.Name>>;
        }
        e.NewNames
      >
}

* Добавляет вызов аварийной функции Func@0 в конец каждой специализируемой
* функции.Этот вызов облегчает отладку при аварийном завершении программы
* в специализированной функции.
CreateGuardFunctions {
  e.Info
    = <MapAccum
        {
          (e.GuardEnums)
          ((e.Name) (e.SpecPattern) (e.Sentences) e.Signatures)
            = <AddSuffix e.Name ('@' 0)> : e.GuardName
            = (e.SpecPattern)
              ((CallBrackets (Symbol Name e.GuardName) e.SpecPattern))
            : e.GuardSentence
            = (
                e.GuardEnums
                (Function GN-Local (e.GuardName) Sentences /* пусто */)
              )
              (
                (e.Name)
                (e.SpecPattern)
                (e.Sentences (e.GuardSentence))
                e.Signatures
              )
        }
        (/* guard enums */) e.Info
      >
}

* Обеспечивает специализацию функций Func*n. Такие функции могут получаться
* при частичной прогонке функций, помеченных как прогоняемые
* и специализированные одновременно. Этот остаток тоже имеет смысл
* специализировать.
CreateRemainderFunctions {
  e.Info = <Map (&CreateRemainderFunctions-OneFunction 0) e.Info>
}

CreateRemainderFunctions-OneFunction {
  s.Count ((e.Name) (e.SpecPattern) ((e.GuardSentence)) 1 /* нет сигнатур */)
    = /* пусто */;

  s.Count ((e.Name) (e.SpecPattern) (e.Sentences) 1 /* нет сигнатур */)
    = s.Count
    : {
        0 = e.Name;
        s._ = <AddSuffix e.Name ('*' s.Count)>;
      }
    : e.RemainderName
    = ((e.RemainderName) (e.SpecPattern) (e.Sentences) 1 /* нет сигнатур */)
      <CreateRemainderFunctions-OneFunction
        <Inc s.Count>
        ((e.Name) (e.SpecPattern) (<Drop e.Sentences>) 1 /* нет сигнатур */)
      >;
}

Drop {
  t.First e.Rest = e.Rest
}

/**
  <OptTree-Spec s.OptSpec e.AST> == e.AST^

  s.OptSpec ::= OptSpec | NoOpt
*/
$ENTRY OptTree-Spec {
  NoOpt e.AST = <ColdAll e.AST>;

  OptSpec e.AST-B (SpecInfo (e.Names) e.SpecInfo) e.AST-E
    = <MapAccum &SpecUnit (e.SpecInfo (/* new functions */)) e.AST-B e.AST-E>
    : (e.SpecInfo^ (e.NewFunctions)) e.AST
    = (SpecInfo (e.Names) e.SpecInfo) e.AST e.NewFunctions;
}

ColdAll {
  e.AST
    = <Map
        {
          (Function s.ScopeClass (e.Name) e.Body)
            = (ColdFunction s.ScopeClass (e.Name) e.Body);

          t.OtherItem = t.OtherItem;
        }
        e.AST
      >;
}

SpecUnit {
  (e.SpecInfo (e.NewFunctions))
  (Function s.ScopeClass t.Name Sentences e.Sentences)
    = <MapAccum
        &SpecSentence
        (e.SpecInfo (e.NewFunctions))
        e.Sentences
      >
    : (e.SpecInfo^ (e.NewFunctions^)) e.Sentences^
    = (e.SpecInfo (e.NewFunctions))
      (ColdFunction s.ScopeClass t.Name Sentences e.Sentences);

  (e.SpecInfo (e.NewFunctions)) t.Other = (e.SpecInfo (e.NewFunctions)) t.Other;
}

/**
  <SpecSentence (e.SpecInfo (e.NewFunctions)) t.Sentence
    == (e.SpecInfo^ (e.NewFunctions^) t.Sentence^

  t.Sentence ::= ((e.Pattern) e.Conditions (e.Result))

  Функция ищет в правой части предложения вызовы специализируемых функций
  и пытается выполнить специализацию для него
*/
SpecSentence {
  (e.SpecInfo (e.NewFunctions))
  ((e.Pattern) e.Conditions (e.Result))
    = <MapAccum
        {
          (e.SpecInfo^ (e.NewFunctions^))
          (Condition (e.Name) (e.ConditionResult) (e.ConditionPattern))
            = <SpecResult (e.SpecInfo (e.NewFunctions)) e.ConditionResult>
            : (e.SpecInfo^ (e.NewFunctions^)) e.ConditionResult^
            = (e.SpecInfo (e.NewFunctions))
              (Condition (e.Name) (e.ConditionResult) (e.ConditionPattern));
        }
        (e.SpecInfo (e.NewFunctions))
        e.Conditions
      >
    : (e.SpecInfo^ (e.NewFunctions^)) e.Conditions^
    = <SpecResult (e.SpecInfo (e.NewFunctions)) e.Result>
    : (e.SpecInfo^ (e.NewFunctions^)) e.Result^
    = (e.SpecInfo (e.NewFunctions)) ((e.Pattern) e.Conditions (e.Result));
}


/**
  <SpecResult (e.SpecInfo (e.NewFunctions)) e.Results>
    == (e.SpecInfo^ (e.NewFunctions^)) e.Result^

  Функция разбирает результирующее выражение, ищет в нем вызовы специализируемых
  функций и замыканий и пытается выполнить специализацию для них
*/
SpecResult {
  (e.SpecInfo (e.NewFunctions)) e.Result
    = <SpecResult-Pass Functions (e.SpecInfo (e.NewFunctions)) e.Result>
    : (e.SpecInfo^ (e.NewFunctions^)) e.Result^
    = <SpecResult-Pass Closures (e.SpecInfo (e.NewFunctions)) e.Result>;
}

SpecResult-Pass {
  s.Pass (e.SpecInfo (e.NewFunctions)) e.Result
    = <MapAccum
        {
          (e.Info (e.NewFuncs)) t.Result-T
            = <SpecTerm s.Pass (e.Info (e.NewFuncs)) t.Result-T>;
        }
        (e.SpecInfo (e.NewFunctions))
        e.Result
      >;
}

/**
  <SpecTerm (e.SpecInfo (e.NewFunctions)) t.Term>
    == (e.SpecInfo^ (e.NewFunctions^)) t.Term^

  Функция ищет в терме вызовы специализируемых функций
  и пытается выполнить специализацию для него
*/
SpecTerm {
  s.Pass (e.SpecInfo (e.NewFunctions))
  (CallBrackets (Symbol Name e.Name) e.Expression)
    , e.Name : e._ '@' 0
    /*
      Аргументы вызовов аварийных функций специализатора специализировать
      нельзя, они должны оставаться неизменными.
    */
    = (e.SpecInfo (e.NewFunctions))
      (CallBrackets (Symbol Name e.Name) e.Expression);

  s.Pass (e.SpecInfo (e.NewFunctions))
  (CallBrackets (Symbol Name e.Name) e.Expression)
    = <SpecResult-Pass s.Pass (e.SpecInfo (e.NewFunctions)) e.Expression>
    : (e.SpecInfo^ (e.NewFunctions^)) e.Expression^

    = s.Pass
    : {
        Functions = &TrySpecCall;
        Closures = &SkipSpecCall;
      }
    : s.TrySpecCall

    = <s.TrySpecCall
        (e.SpecInfo (e.NewFunctions))
        (CallBrackets (Symbol Name e.Name) e.Expression)
      >;

  s.Pass (e.SpecInfo (e.NewFunctions)) (CallBrackets e.Expression)
    = <SpecResult-Pass s.Pass (e.SpecInfo (e.NewFunctions)) e.Expression>
    : (e.SpecInfo^ (e.NewFunctions^)) e.Expression^
    = (e.SpecInfo (e.NewFunctions)) (CallBrackets e.Expression);

  s.Pass (e.SpecInfo (e.NewFunctions)) (ClosureBrackets e.ClosureContent)

    = <SpecResult-Pass s.Pass (e.SpecInfo (e.NewFunctions)) e.ClosureContent>
    : (e.SpecInfo^ (e.NewFunctions^)) e.ClosureContent^

    = s.Pass
    : {
        Functions = &SkipSpecCall;
        Closures = &TrySpecCall;
      }
    : s.TrySpecCall

    = <s.TrySpecCall
        (e.SpecInfo (e.NewFunctions))
        (CallBrackets e.ClosureContent (TkVariable 'e@' 0))
      >
    : (e.SpecInfo^ (e.NewFunctions^))
      (CallBrackets e.ClosureContent^ (TkVariable 'e@' 0))

    = e.ClosureContent
    : {
        (Symbol Name e.OptName) /* пусто */
          = (e.SpecInfo (e.NewFunctions)) (Symbol Name e.OptName);

        e.ClosureContent^
          = (e.SpecInfo (e.NewFunctions))
            (ClosureBrackets e.ClosureContent);
      };


  s.Pass (e.SpecInfo (e.NewFunctions)) (Brackets e.Expression)
    = <SpecResult-Pass s.Pass (e.SpecInfo (e.NewFunctions)) e.Expression>
    : (e.SpecInfo^ (e.NewFunctions^)) e.Expression^
    = (e.SpecInfo (e.NewFunctions)) (Brackets e.Expression);

  s.Pass (e.SpecInfo (e.NewFunctions)) (ADT-Brackets (e.Name) e.Expression)
    = <SpecResult-Pass s.Pass (e.SpecInfo (e.NewFunctions)) e.Expression>
    : (e.SpecInfo^ (e.NewFunctions^)) e.Expression^
    = (e.SpecInfo (e.NewFunctions)) (ADT-Brackets (e.Name) e.Expression);

  s.Pass (e.SpecInfo (e.NewFunctions)) t.Other
   = (e.SpecInfo (e.NewFunctions)) t.Other;
}

TrySpecCall {
  (e.SpecInfo-B ((e.Name) e.Info) e.SpecInfo-E (e.NewFunctions))
  (CallBrackets (Symbol Name e.Name) e.Expression)
    = <SpecCall (e.Name) e.Info (e.Expression)>
    : (e.Info^) t.NewCall e.NewFunction
    = (
        e.SpecInfo-B ((e.Name) e.Info) e.SpecInfo-E
        (e.NewFunctions e.NewFunction)
      )
      t.NewCall;

  (e.SpecInfo (e.NewFunctions)) (CallBrackets (Symbol Name e.Name) e.Expression)
    = (e.SpecInfo (e.NewFunctions))
      (CallBrackets (Symbol Name e.Name) e.Expression);
}

SkipSpecCall {
  (e.SpecInfo (e.NewFunctions)) t.CallTerm
    = (e.SpecInfo (e.NewFunctions)) t.CallTerm;
}

/**
  <SpecCall
    (e.Name) (e.SpecPattern) (e.Body)
    s.CurrentNumber e.Signatures (e.Argument)
  >
    == ((e.SpecPattern) (e.Body) s.NextNumber e.Signatures t.NewSignature)
        t.NewCall t.NewFunction

  Функция пытается выполнить специализацию для заданного вызова:
  # ищет среди сигнатур готовых специализированных функций похожую
    * если сигнатура найдена, заменяем исходный вызов на вызов найденной функции
    * если сигнатура не найдена выполняем специализацию для аргументов вызова
      # формируем новый вызов
      # формируем определение новой специализированной функции
  # возвращает e.Info с новой сигнатурой,
    новый вызов
    определение новой специализированной функции
*/
SpecCall {
  (e.Name) (e.SpecPattern) (e.Body) s.NextNumber e.Signatures (e.Argument)

    , <GenericMatch-Wrapper (e.Argument) (e.SpecPattern)>
    : Clear (e.StaticMatches) (e.DynamicMatches)

    , <ExtractCalls e.StaticMatches> : (e.Calls) e.StaticMatches^

    , <PrepareSignature e.StaticMatches (e.SpecPattern)> : e.Signature

    , <IsTrivialSubstitutions e.StaticMatches> : False

    /* Проверяем, выполняется ли отношение Хигмана-Крускала для текущей сигнатуры и одной из предыдущих */
    , <HasHigmanKruskalRelation (e.Signature) e.Signatures> : False

    = <SpecCall-Aux
        (e.Name) (e.SpecPattern) (e.Body) s.NextNumber e.Signatures (e.Argument)
        (e.Signature) (e.StaticMatches) (e.DynamicMatches) (e.Calls)
      >;

  (e.Name) (e.SpecPattern) (e.Body) s.NextNumber e.Signatures (e.Argument)
    = ((e.SpecPattern) (e.Body) s.NextNumber e.Signatures)
      (CallBrackets (Symbol Name e.Name) e.Argument)
      /* no new functions */;
}

SpecCall-Aux {
  (e.Name) (e.SpecPattern) (e.Body) s.NextNumber e.Signatures (e.Argument)
  (e.Signature) (e.StaticMatches) (e.DynamicMatches) (e.Calls)

    = <PrepareArgOrPattern (e.StaticMatches) (e.DynamicMatches) e.SpecPattern>
    : e.SpecializedFuncArgument

    = <ApplySubst-Expr (e.Calls) e.SpecializedFuncArgument>
    : e.SpecializedFuncArgument^

    = <Spec-FindInSignatures (e.Signature) e.Signatures>
    : {
        Found e.InstanceName
          = ((e.SpecPattern) (e.Body) s.NextNumber e.Signatures)
            (CallBrackets
              (Symbol Name e.InstanceName) e.SpecializedFuncArgument
            )
            /* no new functions */;

        NotFound

          = <PrepareSpecializedFuncBody
              (e.SpecPattern) (e.StaticMatches) e.Body
            >
          : e.SpecializedFuncBody

          , <IsSoundBody e.SpecializedFuncBody>
          : {
              True
                = <AddSuffix e.Name ('@' s.NextNumber)> : e.InstanceName
                = (
                    (e.SpecPattern) (e.Body) <Inc s.NextNumber>
                    e.Signatures ((e.InstanceName) e.Signature)
                  )
                  (CallBrackets
                    (Symbol Name e.InstanceName) e.SpecializedFuncArgument
                  )
                  (Function
                    GN-Local (e.InstanceName) Sentences e.SpecializedFuncBody
                  );

              False
                = ((e.SpecPattern) (e.Body) s.NextNumber e.Signatures)
                  (CallBrackets (Symbol Name e.Name) e.Argument)
                  /* no new functions */;
            }
      }
}

ExtractCalls {
  e.StaticMatches

    = <Map
        {
          (e.Expr ':' t.Var) = <ExtractVariables-Expr e.Expr>;
        }
        e.StaticMatches
      >
    : e.UsedVars

    = <MapAccum
        {
          (e.Calls (e.UsedVars^)) (e.Expr ':' t.Var)
            = <ExtractCalls-Expr (e.Calls (e.UsedVars)) e.Expr>
            : (e.Calls^ (e.UsedVars^)) e.Expr^
            = (e.Calls (e.UsedVars)) (e.Expr ':' t.Var);
        }
        (/* calls */ (e.UsedVars)) e.StaticMatches
      >
    : (e.Calls (e.UsedVars^)) e.StaticMatches^

    = (e.Calls) e.StaticMatches
}

ExtractCalls-Expr {
  (e.Calls (e.UsedVars)) e.Expr
    = <MapAccum &ExtractCalls-Term (e.Calls (e.UsedVars)) e.Expr>;
}

ExtractCalls-Term {
  (e.Calls (e.UsedVars)) (CallBrackets e.Expr)
    = <NewVarName (e.UsedVars) 'eCall' 0> : (e.UsedVars^) e.NewVar
    = (e.Calls ((CallBrackets e.Expr) ':' (e.NewVar)) (e.UsedVars))
      (TkVariable e.NewVar);

  (e.Calls (e.UsedVars)) (Brackets e.Expr)
    = <ExtractCalls-Expr (e.Calls (e.UsedVars)) e.Expr>
    : (e.Calls^ (e.UsedVars^)) e.Expr^
    = (e.Calls (e.UsedVars)) (Brackets e.Expr);

  (e.Calls (e.UsedVars)) (ADT-Brackets (e.Name) e.Expr)
    = <ExtractCalls-Expr (e.Calls (e.UsedVars)) e.Expr>
    : (e.Calls^ (e.UsedVars^)) e.Expr^
    = (e.Calls (e.UsedVars)) (ADT-Brackets (e.Name) e.Expr);

  (e.Calls (e.UsedVars)) t.OtherTerm = (e.Calls (e.UsedVars)) t.OtherTerm;
}

IsSoundBody {
  e.Body
    , <FindClosuresInPatterns-Body e.Body>
    : {
        /* пусто */ = True;
        e.FoundClosures = False;
      };
}

FindClosuresInPatterns-Body {
  e.Body
    = <Map
        {
          ((e.Pattern) e.Conditions (e.Result))
            = <FindClosuresInPatterns-Pattern
                e.Pattern
                <Map
                  {
                    (Condition (e.Name) (e.CondResult) (e.CondPattern))
                      = e.CondPattern;
                  }
                  e.Conditions
                >
              >;
        }
        e.Body
      >;
}

FindClosuresInPatterns-Pattern {
  e.Pattern = <Map &FindClosuresInPatterns-Term e.Pattern>;
}

FindClosuresInPatterns-Term {
  (Symbol s.Type e.Info) = /* пусто */;
  (TkVariable s.Type e.Index) = /* пусто */;

  (Brackets e.Pattern)
    = <FindClosuresInPatterns-Pattern e.Pattern>;
  (ADT-Brackets (e.Name) e.Pattern)
    = <FindClosuresInPatterns-Pattern e.Pattern>;

  (ClosureBrackets e.Context) = Found;
}

PrepareSignature {
  e.StaticMatches (e.SpecPattern)
    /* ExtractVariables-Expr сохраняет порядок переменных в выражении */
    = <ExtractVariables-Expr e.SpecPattern> : e.SpecVars
    = <PrepareSignature-SortVars e.StaticMatches (e.SpecVars)> : e.ExprList
    = <RenameSignatureVars e.ExprList>;
}

PrepareSignature-SortVars {
  e.StaticMatches (e.SpecPatternVars)
    = <MapAccum
        {
          (e.Vars-B (e.Val ':' (TkVariable e.Index)) e.Vars-E) (e.Index)
            = (e.Vars-B e.Vars-E) (e.Val);

          /*
            Если очередной переменной нет среди подстановок, значит
            она динамическая
          */
          (e.Variables) (e.Index) = (e.Variables) /* пропускаем */;
        }
        (e.StaticMatches) e.SpecPatternVars
      >
    : (/* должно быть пусто */) e.SigValues
    = e.SigValues;
}

RenameSignatureVars {
  e.Signature
    = /* пусто */ : e.KnownVars
    = 0 : s.NextId
    = <MapAccum
        {
          (e.KnownVars^ s.NextId^) (e.SignaturePart)
            = <RenameSignatureVars-Expr (e.KnownVars s.NextId) e.SignaturePart>
            : (e.KnownVars^ s.NextId^) e.SignaturePart^
            = (e.KnownVars s.NextId) (e.SignaturePart);
        }
        (e.KnownVars s.NextId)
        e.Signature
      >
    : (e.KnownVars^ s.NextId^) e.Signature^
    = e.Signature;
}

RenameSignatureVars-Expr {
  t.State e.Expr = <MapAccum &RenameSignatureVars-Term t.State e.Expr>;
}

RenameSignatureVars-Term {
  (e.KnownVariables s.NextId) (TkVariable s.Mode e.OldIndex)
    , e.KnownVariables : e._B (s.Mode e.OldIndex (e.NewIndex)) e._E
    = (e.KnownVariables s.NextId)
      (TkVariable s.Mode e.NewIndex);

  (e.KnownVariables s.NextId) (TkVariable s.Mode e.OldIndex)
    = <Symb s.NextId> 0 : e.NewIndex
    = (e.KnownVariables (s.Mode e.OldIndex (e.NewIndex)) <Inc s.NextId>)
      (TkVariable s.Mode e.NewIndex);

  t.State (s.BracketsTag e.Expr)
    , <OneOf s.BracketsTag CallBrackets ClosureBrackets Brackets> : True
    = <RenameSignatureVars-Expr t.State e.Expr> : t.State^ e.Expr^
    = t.State (s.BracketsTag e.Expr);

  t.State (ADT-Brackets (e.Name) e.Expr)
    = <RenameSignatureVars-Expr t.State e.Expr> : t.State^ e.Expr^
    = t.State (ADT-Brackets (e.Name) e.Expr);

  t.State t.OtherTerm = t.State t.OtherTerm;
}

/*
  Применяем левую подстановку к левым частям правой.
  Это не совсем композиция подстановок, поскольку для композиции нужно
  также объединять домены подстановок.
*/
ApplySubst-Subst {
  (e.FromSubstitution) e.ToSubstitution
    = <Map
        {
          (e.Expr ':' t.Var)
            = (<ApplySubst-Expr (e.FromSubstitution) e.Expr> ':' t.Var);
        }
        e.ToSubstitution
      >;
}

ApplySubst-Expr {
  (e.Substitution) e.Expression
    = <Map
        {
          (TkVariable e.ModeIndex)
            , e.Substitution : e.Subst-B (e.Value ':' (e.ModeIndex)) e.Subst-E
            = e.Value;

          (Brackets e.Nested)
            = (Brackets <ApplySubst-Expr (e.Substitution) e.Nested>);

          (ADT-Brackets (e.Name) e.Nested)
            = (ADT-Brackets
                (e.Name) <ApplySubst-Expr (e.Substitution) e.Nested>
              );

          (CallBrackets e.Nested)
            = (CallBrackets <ApplySubst-Expr (e.Substitution) e.Nested>);

          (ClosureBrackets e.ClosureContent)
            = (ClosureBrackets
                <ApplySubst-Expr (e.Substitution) e.ClosureContent>
              );

          t.OtherTerm = t.OtherTerm;
        }
        e.Expression
      >;
}

Spec-FindInSignatures {
  (e.Signature) e.Signatures-B ((e.InstanceName) e.Signature) e.Signatures-E
    = Found e.InstanceName;

  (e.Signature) e.Signatures
    = NotFound;
}

/**
  <IsTrivialSubstitutions e.StaticMatches>
*/
IsTrivialSubstitutions {
  e.StaticMatches
    = <Reduce
        {
          False t.Any = False;

          True ((TkVariable s.Mode e.Index1) ':' (TkVariable s.Mode e.Index2))
            = True;

          True t.OtherSubstitution = False;
        }
        True
        e.StaticMatches
      >;
}

PrepareArgOrPattern {
  (e.StaticMatches) (e.DynamicMatches) e.SpecPattern

    /*
      ExtractVariables-Expr сохраняет относительный порядок переменных,
      однако оставляет дубликаты. Но в e.SpecPattern это не критично,
      поскольку все переменные в нём должны быть уникальны (иначе
      синтаксическая ошибка).
    */
    = <ExtractVariables-Expr e.SpecPattern> : e.SpecVars

    = <MapAccum
        {
          (e.KnownVarsFromStatic) (s.Mode e.Index)
            , e.StaticMatches
            : e._B (e.Expr ':' (TkVariable s.Mode e.Index)) e._E
            = <MapAccum
                {
                  (e.KnownVarsFromStatic^) (s.Mode^ e.Index^)
                    , e.KnownVarsFromStatic : e._B1 (s.Mode e.Index) e._E1
                    = (e.KnownVarsFromStatic)
                      /* пропускаем переменную */;

                  (e.KnownVarsFromStatic^) (s.Mode^ e.Index^)
                    = (e.KnownVarsFromStatic (s.Mode e.Index))
                      <WrapVar s.Mode (TkVariable s.Mode e.Index)>;
                }
                (e.KnownVarsFromStatic)
                <ExtractVariables-Expr e.Expr>
              >;

          (e.KnownVarsFromStatic) (s.Mode e.Index)
            , e.DynamicMatches
            : e._B (e.Expr ':' (TkVariable s.Mode e.Index)) e._E
            = (e.KnownVarsFromStatic) <WrapVar s.Mode e.Expr>;
        }
        (/* Known vars from static */)
        e.SpecVars
      >
    : (e.KnownVarsFromStatic) e.NewArgOrPattern

    = <RemoveLastGhostBracket e.NewArgOrPattern>;
}

WrapVar {
  'e' e.Expr = (GhostBrackets e.Expr);
  s.Mode e.Expr = e.Expr
}

RemoveLastGhostBracket {
  e.NewArgOrPattern (GhostBrackets e.LastEValue)
    = <Map
        {
          (GhostBrackets e.Expr) = (Brackets e.Expr);
          t.STValue = t.STValue;
        }
        e.NewArgOrPattern
      >
      e.LastEValue;

  e.NewArgOrPattern t.STValue
    = <RemoveLastGhostBracket e.NewArgOrPattern> t.STValue;

  /* пусто */ = /* пусто */;
}

RenameGenericMatch {
  (e.SpecPattern) (e.StaticMatches) e.VarsFromBody
    = <Map
        {
          (e.Val ':' t.Var) = <ExtractVariables-Expr e.Val>;
        }
        e.StaticMatches
      >
    : e.VarsFromMatch

    = <Unique e.VarsFromMatch> : e.VarsFromMatch^

    = <MapAccum
        {
          (e.VarsFromBody^) (e.CurrentVarName)
            = <NewVarName (e.VarsFromBody) e.CurrentVarName>
            : (e.VarsFromBody^) e.NewVarName
            = (e.VarsFromBody)
              ((TkVariable e.NewVarName) ':' (e.CurrentVarName))
        }
        (e.VarsFromBody)
        e.VarsFromMatch
      >
    : (e.VarsFromBody^) e.Renames

    = <ApplySubst-Subst (e.Renames) e.StaticMatches>;
}

PrepareSpecializedFuncBody {
  (e.SpecPattern) (e.StaticMatches) e.Body
    = <ExtractVariables e.Body> : e.BodyVariables

    = <RenameGenericMatch (e.SpecPattern) (e.StaticMatches) e.BodyVariables>
    : e.StaticMatches^

    = <Map (&PrepareSpecSentence (e.SpecPattern) (e.StaticMatches)) e.Body>;
}

PrepareSpecSentence {
  (e.SpecPattern) (e.CallStaticMatches)
  ((e.Pattern) e.Conditions (e.Result))
    = <GenericMatch-Wrapper (e.Pattern) (e.SpecPattern)>
    : Clear (e.PatternStaticMatches) (e.PatternDynamicMatches)

    = <MapGenericMatches (e.CallStaticMatches) (e.PatternStaticMatches)>
    : e.StaticSubstitutions

    = <ApplySubst-Subst (e.StaticSubstitutions) e.PatternDynamicMatches>
    : e.PatternDynamicMatches^

    = <PrepareArgOrPattern
        (e.CallStaticMatches) (e.PatternDynamicMatches) e.SpecPattern
      >
    : e.Pattern^

    = <PrepareSpecConditions (e.StaticSubstitutions) e.Conditions>
    : e.Conditions^

    = <ApplySubst-Expr (e.StaticSubstitutions) e.Result> : e.Result^

    = ((e.Pattern) e.Conditions (e.Result));
}

PrepareSpecConditions {
  (e.StaticSubstitutions) e.Conditions
    = <Map
        {
          (Condition (e.Name) (e.ConditionResult) (e.ConditionPattern))
            = (Condition
                (e.Name)
                (<ApplySubst-Expr (e.StaticSubstitutions) e.ConditionResult>)
                (<ApplySubst-Expr (e.StaticSubstitutions) e.ConditionPattern>)
              );
        }
        e.Conditions
      >
}

/**
  <OptTree-Spec-CleanupColdFunctions e.AST> == e.AST^
*/
$ENTRY OptTree-Spec-CleanupColdFunctions {
  e.AST
    = <Map
        {
          (ColdFunction s.ScopeClass (e.Name) e.Body)
            = (Function s.ScopeClass (e.Name) e.Body);

          t.OtherItem = t.OtherItem;
        }
        e.AST
      >;
}

/*
  Вспомогательные функции модуля
*/

GenericMatch-Wrapper {
  (e.Expr) (e.HardExpr)
    = <GenericMatch (e.Expr) (e.HardExpr)>
    : {
        Clear e.VarsMatches
          = <MapAccum
              {
                (e.StaticMatches) (e.Val ':' (e.Var))
                  , e.Var : s.Mode e.Index
                  , <IsSpecStaticVar (e.Var)> : True
                  = (e.StaticMatches (e.Val ':' (TkVariable e.Var)))
                    /* пусто */;

                (e.StaticMatches) (e.Val ':' (e.Var))
                  = (e.StaticMatches)
                    (e.Val ':' (TkVariable e.Var));
              }
              (/* for statics */)
              e.VarsMatches
            >
          : (e.StaticMatches) e.DynamicMatches
          = Clear (e.StaticMatches) (e.DynamicMatches);

        s.OtherResult e.ResultInfo
          = s.OtherResult e.ResultInfo;
      };
}

IsSpecStaticVar {
  (s.Mode s.FirstSymbol e.Index)
    , <Type s.FirstSymbol>
    : {
        'Lu' s._ = True;
        e._ = False;
      };
}

MapGenericMatches {
  (e.ArgStaticMatches) (e.PatternStaticMatches)
    = <Reduce
        {
          (
            (e.ArgStaticMatches-B (e.ArgVal ':' t.SpecVar) e.ArgStaticMatches-E)
            (e.MappedMatches)
          )
          ((TkVariable s.Mode e.Index) ':' t.SpecVar)
            = (
                (
                  e.ArgStaticMatches-B (e.ArgVal ':' t.SpecVar)
                  e.ArgStaticMatches-E
                )
                (e.MappedMatches (e.ArgVal ':' (s.Mode e.Index)))
              );
        }
        ((e.ArgStaticMatches) (/* for result */))
        e.PatternStaticMatches
      >
    : ((e.ArgStaticMatches) (e.ResultMatches))
    = e.ResultMatches;
}

/*
  Функции, относящиеся к проверке отношения Хигмана-Крускала
*/

/**
  <HasHigmanKruskalRelation (e.Signature) e.Signatures> == True e.InstanceName | False

  Функция проверяет, выполняется ли отношение Хигмана-Крускала для одной из сигнатур в
  e.Signatures и сигнатуры e.Signature, то есть проверяется, можно ли вложить одну сигнатуру в другую,
  удалив некоторые элементы.
  В случае, если в истории сигнатур удаётся найти сигнатуру, для которой выполняется указанное отношение,
  функция возвращает True и соответствующий e.InstanceName. В ином случае возвращается False.
*/
HasHigmanKruskalRelation {
  (e.Signature) e.Signatures

    /*
      Находим в истории сигнатур ещё не проверенную сигнатуру, поиск начинаем с конца.
      Скобки с Checked добавляются, чтобы данный образец работал для первого вызова функции.
    */
    , e.Signatures (Checked (/*пусто*/) (/*пусто*/))
    : e.Signatures-B ((e.NextInstanceName) e.NextSignature)
      (Checked (e.InstanceName) e.CheckedSignature) e.Signature-E

    /* Проверяем для пары сигнатур отношение Хигмана-Крускала */
    , <CheckSignaturesPairForRelation (e.Signature) e.NextSignature> : True

    /* Если для этой пары сигнатур отношение выполнилось, возвращаем True и e.InstanceName */
    = True e.NextInstanceName;

  /*
    Если попали сюда, то :
    1) либо для текущей сигнатуры из истории отношение не выполнилось, это значит,
    что надо пометить текущую сигнатуру как Checked и перейти к следующей,
    2) либо все сигнатуры из истории уже проверены и надо вернуть False.
  */
  /* Проверяем 1) */
  (e.Signature) e.Signatures

    , e.Signatures (Checked (/*пусто*/) (/*пусто*/))
    : e.Signatures-B ((e.NextInstanceName) e.NextSignature)
      (Checked (e.InstanceName) e.CheckedSignature) e.Signature-E

    /* Указываем Checked для сигнатуры и выполняем рекурсию */
    = e.Signatures-B (Checked (e.NextInstanceName) e.NextSignature)
      (Checked (e.InstanceName) e.CheckedSignature) e.Signature-E
    : e.Signatures^

    = <HasHigmanKruskalRelation (e.Signature) e.Signatures>;

  /* Получили ситуацию 2), возвращаем False */
  (e.Signature) e.Signatures
    = False;
}

/**
  <CheckSignaturesPairForRelation (e.CurrentSignature) e.HistorySignature> == True | False

  Проверяет выполнение отношения Хигмана-Крускала для двух сигнатур.
  Возвращает True, если отношение выполняется, и False в ином случае.
*/
CheckSignaturesPairForRelation {
  (e.CurrentSignature) e.HistorySignature
    /* Запускаем рекурсивный процесс */
    = <DoCheckSignaturesPairForRelation (1) (e.CurrentSignature) e.HistorySignature>;
}

/* Одна итерация функции CheckSignaturesPairForRelation */
DoCheckSignaturesPairForRelation {
  (s.ElemNumber) (e.CurrentSignature) e.HistorySignature

    /* Проверяем, равны ли сигнатуры с точностью до переименования переменных в текущей сигнатуре */
    , <EqSignatures (e.CurrentSignature) e.HistorySignature> : True

    = True;

  (s.ElemNumber) (e.CurrentSignature) e.HistorySignature

    /* Удаляем элемент, который при проходе по элементам текущей сигнатуры попадётся под номером s.ElemNumber */
    = <DeleteElemWithNumber (s.ElemNumber) e.CurrentSignature> :
      {
        True e.SignatureWithDelElem
          /* Выполняем рекурсию с сигнатурой с удалённым элементом */
          , <DoCheckSignaturesPairForRelation (s.ElemNumber) (e.SignatureWithDelElem) e.HistorySignature>
          : True
          = True;

        True e.SignatureWithDelElem
          /* Не удаляем текущий элемент и переходим к следующему */
          = <DoCheckSignaturesPairForRelation (<Inc s.ElemNumber>) (e.CurrentSignature) e.HistorySignature>;

        e.Expr = False;
      }
}

/**
  <EqSignatures (e.CurrentSignature) e.HistorySignature> == True | False

  Проверяет, равны ли сигнатуры с точностью до переименования переменных в текущей сигнатуре.
*/
EqSignatures {
  (e.CurrentSignature) e.HistorySignature

    /* Выполняем переименование переменных в текущей сигнатуре */
    = <RenameSignatureVars e.CurrentSignature> : e.CurrentSignature^

    /* Проверяем, равны ли сигнатуры */
    = e.CurrentSignature
    : {
        e.HistorySignature = True;
        e.OtherSignature = False;
      }
}

/**
  <DeleteElemWithNumber (s.ElemNumber) e.Signature> == True e.Signature^ | False e.Signature^

  Удаляет из сигнатуры элемент, который при проходе через сигнатуру попадётся под указанным номером.
  Первым параметром возвращает True или False в зависимости от того, удалось ли
  удалить элемент с указанным номером.
  Если не удалось (значение False), то элемента с таким номером нет.
*/
DeleteElemWithNumber {
  (s.ElemNumber) e.Signature
    = False : s.Changed
    = 1 : s.CurNumber
    = <MapAccum
        {
          (s.ElemNumber^ s.CurNumber^ s.Changed^) (e.SignaturePart)
            = <DeleteElemWithNumber-Expr (s.ElemNumber s.CurNumber s.Changed) e.SignaturePart>
            : (s.ElemNumber^ s.CurNumber^ s.Changed^) e.SignaturePart^
            = (s.ElemNumber s.CurNumber s.Changed) (e.SignaturePart);
        }
        (s.ElemNumber s.CurNumber s.Changed)
        e.Signature
      >
    : (s.ElemNumber^ s.CurNumber^ s.Changed^) e.Signature^
    = s.Changed e.Signature;
}

DeleteElemWithNumber-Expr {
  t.State e.Expr
    = <MapAccum &DeleteElemWithNumber-Term t.State e.Expr>;
}

DeleteElemWithNumber-Term {
  /* Если s.Changed == True, ничего не делаем, так как нужный элемент уже был удалён */
  (s.ElemNumber s.CurNumber s.Changed) e.Expr
    , s.Changed : True
    = (s.ElemNumber s.CurNumber s.Changed) e.Expr;

  (s.ElemNumber s.CurNumber s.Changed) (TkVariable s.Mode e.Index)
    , <Compare s.ElemNumber s.CurNumber> : '0'
    = True : s.Changed^
    = (s.ElemNumber <Inc s.CurNumber> s.Changed);

  (s.ElemNumber s.CurNumber s.Changed) (s.BracketsTag e.Expr)
    , <OneOf s.BracketsTag CallBrackets ClosureBrackets Brackets> : True
    , <Compare s.ElemNumber s.CurNumber> : '0'
    = True : s.Changed^
    = (s.ElemNumber <Inc s.CurNumber> s.Changed) e.Expr;

  (s.ElemNumber s.CurNumber s.Changed) (s.BracketsTag e.Expr)
    , <OneOf s.BracketsTag CallBrackets ClosureBrackets Brackets> : True
    = <DeleteElemWithNumber-Expr (s.ElemNumber <Inc s.CurNumber> s.Changed) e.Expr> : (s.ElemNumber^ s.CurNumber^ s.Changed^) e.Expr^
    = (s.ElemNumber s.CurNumber s.Changed) (s.BracketsTag e.Expr);

  (s.ElemNumber s.CurNumber s.Changed) (ADT-Brackets (e.Name) e.Expr)
    , <Compare s.ElemNumber s.CurNumber> : '0'
    = True : s.Changed^
    = (s.ElemNumber <Inc s.CurNumber> s.Changed) e.Expr;

  (s.ElemNumber s.CurNumber s.Changed) (ADT-Brackets (e.Name) e.Expr)
    = <DeleteElemWithNumber-Expr (s.ElemNumber <Inc s.CurNumber>  s.Changed) e.Expr> : (s.ElemNumber^ s.CurNumber^ s.Changed^) e.Expr^
    = (s.ElemNumber s.CurNumber s.Changed) (ADT-Brackets e.Expr);

  t.State e.Expr = t.State e.Expr;
}
