$INCLUDE "LibraryEx";

*$FROM DisplayName
$EXTERN DisplayName;

*$FROM OptTree-AutoMarkup-Common
$EXTERN OptTree-AutoMarkup-Contains,
        OptTree-AutoMarkup-GetElemByKey;

/**
  <OptTree-AutoMarkup-ExtractGraph e.AST> == e.FunctionCallsGraph
*/

$ENTRY OptTree-AutoMarkup-ExtractFunctionCallsGraph {
  e.AST
    = <MapAccum
        {
          ((e.GraphNodes) (e.IndirectChildren))
          t.AstNode
            = <GetFuncs t.AstNode> : e.NodeOpt (e.NodeIndirectChildren)
            = (
                (e.GraphNodes e.NodeOpt)
                (e.IndirectChildren e.NodeIndirectChildren)
              );
        }
        ((/*graph nodes*/) (/*indirect children*/)) e.AST
      >
      : ((e.ExtractedFuncs) (e.IndirectChildren))
     = <Map &GetMetatablesNames e.AST>
      : e.MetatablesNames
    = <Unique e.IndirectChildren>
      : e.UniqueIndirectChildren
    = <Map
        {
          (e.FuncName)
            , e.MetatablesNames : e.MetatableHead (e.FuncName) e.MetatableTail
            = ;

          e.Other
            = e.Other;
        }
        e.UniqueIndirectChildren
      >
      : e.ClearedUniqueIndirectChildren
    = (Func
        (Indirect function for implicit calls)
        Children (e.ClearedUniqueIndirectChildren)
      )
      : t.IndirectFuncNode
    = <Map &GetMetatables e.AST>
      : e.ExtractedMetaTables
    = <InlineFuncCallsWithMetatable
        (e.ExtractedFuncs)
        (e.ExtractedMetaTables)
      >
      : e.InlinedGraph
    = <Map &GetEntryPoints e.AST>
      : e.EntryPoints
    = e.EntryPoints e.UniqueIndirectChildren
      : e.EntryPoints^
    = <GetClearedByEntryPointsGraph
        (e.InlinedGraph)
        (e.EntryPoints)
      >
      : e.ResultGraph
    = t.IndirectFuncNode e.ResultGraph;
}

GetEntryPoints {
  (Function GN-Entry (e.FuncName) Sentences e.Body)
    = (e.FuncName);

  (Function s.Scope (e.FuncName) Sentences e.Body)
    , <OneOf (e.FuncName) ('INIT') ('FINAL')> : True
    = (e.FuncName);

  t._ = /* пусто */;
}

GetClearedByEntryPointsGraph {
  (e.Graph) (e.EntryPoints)
    = <GetClearedByEntryPointsGraphRec
        (e.Graph)
        ()
        (e.EntryPoints)
      >
    : ((e.UsedFuncNames) (e.Graph^))
    = <Map
        {
          (Func (e.FuncName) Children (e.Children))
            , e.UsedFuncNames : e._ (e.FuncName) e._
            = (Func (e.FuncName) Children (e.Children));

          (Func (e._) Children (e._)) = /* пропускаем */;
        }
        e.Graph
      >
}

GetClearedByEntryPointsGraphRec {
  (e.Graph) (e.ProcessedNodes) (e.EntryPoints)
    = <DoGetClearedByEntryPointsGraphRec
        (e.ProcessedNodes) (/* result */) (e.Graph) e.EntryPoints
      >;
}

/*
  Явный цикл вместо MapAccum нужен ради экономии числа шагов. Неоптимизированный
  MapAccum требует дополнительных шагов на Mu, Apply и присваивание внутри
  DoMapAccum.
*/
DoGetClearedByEntryPointsGraphRec {
  (e.ProcessedNodes-B (e.EntryPoint) e.ProcessedNodes-E)
  (e.Result) (e.Graph)
  (e.EntryPoint) e.EntryPoints
    = <DoGetClearedByEntryPointsGraphRec
        (e.ProcessedNodes-B (e.EntryPoint) e.ProcessedNodes-E)
        (e.Result) (e.Graph)
        e.EntryPoints
      >;

  (e.ProcessedNodes) (e.Result) (e.Graph)
  (e.EntryPoint) e.EntryPoints
    = e.ProcessedNodes (e.EntryPoint) : e.NewProcessedNodes
    = <OptTree-AutoMarkup-GetElemByKey
        (e.EntryPoint)
        (e.Graph)
      >
    : e.Node (e.Graph^)
    = e.Node
    : {
        Func (e.FuncName) Children (e.Children)
          = <GetClearedByEntryPointsGraphRec
              (e.Graph) (e.NewProcessedNodes) (e.Children)
            >
          : ((e.SubTreeResult) (e.Graph^))
          = <DoGetClearedByEntryPointsGraphRec
              (e.NewProcessedNodes)
              (<UnionWith (e.FuncName) e.SubTreeResult (e.Result)>)
              (e.Graph)
              e.EntryPoints
            >;

        e._
          = <DoGetClearedByEntryPointsGraphRec
              (e.NewProcessedNodes) (e.Result) (e.Graph) e.EntryPoints
            >;
      };

  (e.ProcessedNodes) (e.Result) (e.Graph) /* пусто */
    = ((e.Result) (e.Graph));
}

/*
  <UnionWith e.SmallSet (e.BigSet)> == e.Set
*/
UnionWith {
  e.Small-Unique t.Item e.Small-Unscanned (e.Big-B t.Item e.Big-E)
    = e.Small-Unique <UnionWith e.Small-Unscanned (t.Item e.Big-B e.Big-E)>;

  e.SmallSet (e.BigSet) = e.SmallSet e.BigSet;
}

GetMetatables {
  (Function s.ScopeClass (e.Name) Metatable e.Metatable)
    = <Map
        &ExtractFunctionCallsFromMetatable
        e.Metatable
      >
      : e.FunctionCalls
    = (e.Name (<Unique e.FunctionCalls>));

  t._other_node = /* пусто */;
}

GetMetatablesNames {
  (Function s.ScopeClass (e.Name) Metatable e.Metatable)
    = (e.Name);

  t._other_node = /* пусто */;
}

GetFuncs {
  (Function s.ScopeClass (e.Name) Sentences e.Body)
    = <MapAccum
        {
          ((e.DirectCalls)(e.ImplicitCalls)(e.IndirectChildren))
          (e.Sentence)
            = <ExtractFunctionCallsFromSentencesBody
                (e.Sentence)
              >
              : (e.SentenceDirectCalls)
                (e.SentenceImplicitCalls)
                (e.SentenceIndirectChildren)
            = (
                (e.DirectCalls e.SentenceDirectCalls)
                (e.ImplicitCalls e.SentenceImplicitCalls)
                (e.IndirectChildren e.SentenceIndirectChildren)
              )
        }
        ((/*direct calls*/) (/*implicit calls*/) (/*indirect children*/))
        e.Body
      >
      : ((e.DirectCalls) (e.ImplicitCalls) (e.IndirectChildren))
    = <Unique e.DirectCalls> : e.UniqueDirectCalls
    = (e.ImplicitCalls)
      : {
          ()
            = ;

          (e._)
            = (Indirect function for implicit calls)
        }
        : e.IndirectChild
    = (Func (e.Name) Children (e.UniqueDirectCalls e.IndirectChild))
      (e.IndirectChildren);

  t._other_node = /* нет узлов */ (/* нет косвенных потомков */);
}

ExtractFunctionCallsFromMetatable {
  ((Symbol Identifier e.Ident) (Symbol Name e.Name))
    = (e.Name);
}

ExtractFunctionCallsFromSentencesBody {
  ((e.Pattern) e.Conditions (e.Result))
    = <ExtractExpressionName e.Pattern>
      : (e.PatternNames) (e.PatternIndirectCalls) (e.PatternIndirectChildren)
    = <MapAccum
        {
          ((e.DirectCalls)(e.IndirectCalls)(e.IndirectChildren))
          (Condition (e.Name) (e.ConditionResult) (e.ConditionPattern))
              = <ExtractExpressionName
                  e.ConditionResult
                >
                : (e.ConditionResultCalls)
                  (e.ConditionResultIndirectCalls)
                  (e.ConditionResultIndirectChildren)
              = <ExtractExpressionName
                  e.ConditionPattern
                >
                : (e.ConditionPatternCalls)
                  (e.ConditionPatternIndirectCalls)
                  (e.ConditionPatternIndirectChildren)
              = (
                  (
                    e.DirectCalls
                    e.ConditionResultCalls
                    e.ConditionPatternCalls
                  )
                  (
                    e.IndirectCalls
                    e.ConditionResultIndirectCalls
                    e.ConditionPatternIndirectCalls
                  )
                  (
                    e.IndirectChildren
                    e.ConditionResultIndirectChildren
                    e.ConditionPatternIndirectChildren
                  )
                );
        }
        ((/*direct calls*/) (/*indirect calls*/) (/*indirect children*/))
        e.Conditions
      >
      : (
          (e.ConditionNames)
          (e.ConditionIndirectCalls)
          (e.ConditionIndirectChildren)
        )
    = <ExtractExpressionName e.Result>
      : (e.ResultNames) (e.ResultIndirectCalls) (e.ResultIndirectChildren)
    = (
        e.PatternNames
        e.ConditionNames
        e.ResultNames
      )
      (
        e.PatternIndirectCalls
        e.ConditionIndirectCalls
        e.ResultIndirectCalls
      )
      (
        e.PatternIndirectCalls
        e.ConditionIndirectChildren
        e.ResultIndirectChildren
      );
}

InnerExtractExpressionName {
  (e.Expr) (e.DirectCalls) (e.IndirectDetected) (e.IndirectChildren)
    = <MapAccum
        {
          ((e.DirectCalls^) (e.IndirectDetected^) (e.IndirectChildren^))
          (Symbol Name e.Name)
            = (
                (e.DirectCalls (e.Name))
                (e.IndirectDetected)
                (e.IndirectChildren)
              );

          ((e.DirectCalls^) (e.IndirectDetected^) (e.IndirectChildren^))
          (t.Marker e.Nested)
            , <OneOf
                (t.Marker)
                (Brackets)
                (ADT-Brackets)
                (ClosureBrackets)
              > : True
            = <InnerExtractExpressionName
                (e.Nested)
                (e.DirectCalls)
                (e.IndirectDetected)
                (e.IndirectChildren)
              >;

          ((e.DirectCalls^) (e.IndirectDetected^) (e.IndirectChildren^))
          (CallBrackets
            (Symbol Name e.Name)
            e.NestedHead
            (Symbol Name e.IndirectChild)
            e.NestedTail
          )
            = (e.IndirectChild)
              <Map
                {
                  (Symbol Name e.NextInderectChild)
                    = (e.NextInderectChild);

                  e.Other
                    = ;
                }
                e.NestedTail
              >
              : e.AdditionaIndirectChildren
            = <Map
                {
                  (Symbol Name e._)
                    = ;

                  e.Other
                    = e.Other;
                }
                e.NestedHead e.NestedTail
              >
              : e.Nested^
            = <InnerExtractExpressionName
                (e.Nested)
                (e.DirectCalls)
                (e.IndirectDetected)
                (e.IndirectChildren)
              >
              : (
                  (e.DirectCalls^)
                  (e.IndirectDetected^)
                  (e.IndirectChildren^)
                )
            = (
                (e.DirectCalls (e.Name))
                (e.IndirectDetected)
                (e.IndirectChildren e.AdditionaIndirectChildren)
              );

          ((e.DirectCalls^) (e.IndirectDetected^) (e.IndirectChildren^))
          (CallBrackets (Symbol Name e.Name) e.Nested)
            = <InnerExtractExpressionName
                (e.Nested)
                (e.DirectCalls)
                (e.IndirectDetected)
                (e.IndirectChildren)
              >
              : (
                  (e.DirectCalls^)
                  (e.IndirectDetected^)
                  (e.IndirectChildren^)
                )
            = (
                (e.DirectCalls (e.Name))
                (e.IndirectDetected)
                (e.IndirectChildren)
              );

          ((e.DirectCalls^) (e.IndirectDetected^) (e.IndirectChildren^))
          (CallBrackets e.Nested)
            = <InnerExtractExpressionName
                (e.Nested)
                (e.DirectCalls)
                (e.IndirectDetected)
                (e.IndirectChildren)
              >
              : (
                  (e.DirectCalls^)
                  (e.IndirectDetected^)
                  (e.IndirectChildren^)
                )
            = (
                (e.DirectCalls)
                (Indirect detected)
                (e.IndirectChildren)
              );

          ((e.DirectCalls^) (e.IndirectDetected^) (e.IndirectChildren^))
          (t.Marker e.Smth)
            = ((e.DirectCalls) (e.IndirectDetected) (e.IndirectChildren));
        }
        ((e.DirectCalls) (e.IndirectDetected) (e.IndirectChildren))
        e.Expr
      >;
}

ExtractExpressionName {
  e.Expr
    = <InnerExtractExpressionName (e.Expr) () () ()>
      : ((e.DirectCalls) (e.IndirectDetected) (e.IndirectChildren))
    = (e.DirectCalls) (e.IndirectDetected) (e.IndirectChildren)
}

InlineFuncCallsWithMetatable {
  (e.ExtractedFuncs) (e.ExtractedMetaTables)
    = <Map
        {
          e.Elem
            = <InlineOneFuncChildrenWithMetatable
                e.Elem
                (e.ExtractedMetaTables)
              >;
        }
        e.ExtractedFuncs
      >;
}

InlineOneFuncChildrenWithMetatable {
  (Func (e.Name) Children (e.UniqueFunctionCalls)) (e.ExtractedMetaTables)
      = <Map
          {
            t.Elem
              = <GetCallsFromMetaTables
                  t.Elem
                  (e.ExtractedMetaTables)
                >;
          }
          e.UniqueFunctionCalls
        >
        : e.ReplacedChildren
      = <Unique e.ReplacedChildren>
        : e.UniqueReplacedChildren
      = (Func (e.Name) Children (e.UniqueReplacedChildren));
}

GetCallsFromMetaTables {
  (e.FuncName) (e.ExtractedMetaTables)
    = (<Map
        {
          (e.Key (e.Value))
            , e.Key : e.FuncName
            = e.Value;

          (e.Other)
            = ;
        }
        e.ExtractedMetaTables
      >)
      : e.ReplaceValue
    = <Fetch
        e.ReplaceValue
        {
          ()
            = (e.FuncName);

          (e.FuncSet)
            = e.FuncSet;
        }
      >;
}
