$INCLUDE "LibraryEx";

*$FROM Config
$EXTERN Config-GetTreeOptCycles, Config-GetOptDrive, Config-GetOptSpec,
  Config-GetOptAutoMarkup, Config-GetOptIntrinsic;

*$FROM OptTree-Drive
$EXTERN OptTree-Drive-ExtractOptInfo, OptTree-Drive,
  OptTree-Drive-CleanupColdCalls;

*$FROM OptTree-Spec
$EXTERN OptTree-Spec-ExtractOptInfo, OptTree-Spec,
  OptTree-Spec-CleanupColdFunctions;

*$FROM Log
$EXTERN Log-PutLine, Log-AST;

*$FROM DisplayName
$EXTERN DisplayName;


/**
  <OptTree t.Config e.AST> == e.OptAST

  e.OptAST не содержит разметки, описывающей прогоняемые, встраиваемые
  и специализируемые функции.
*/
$ENTRY OptTree {
  t.Config e.AST
    = <Config-GetTreeOptCycles t.Config> : s.Cycles
    = <Config-GetOptDrive t.Config> : s.OptDrive
    = <Config-GetOptSpec t.Config> : s.OptSpec
    = <Config-GetOptAutoMarkup t.Config> : s.OptAutoMarkup
    = <Config-GetOptIntrinsic t.Config> : s.OptIntrinsic
    = <OptTree-Aux
        s.Cycles s.OptDrive s.OptIntrinsic s.OptSpec s.OptAutoMarkup e.AST
      >;
}

OptTree-Aux {
  s.Cycles s.OptDrive s.OptIntrinsic s.OptSpec s.OptAutoMarkup e.AST

    = <OptTree-Drive-ExtractOptInfo s.OptDrive s.OptIntrinsic e.AST>
    : ((e.DriveFuncNames) e.DriveInfo-Specific) e.AST^

    = <OptTree-Spec-ExtractOptInfo s.OptSpec e.AST>
    : ((e.SpecFuncNames) e.SpecInfo-Specific) e.AST^

    = <PrintFuncNames ('Drive functions:') e.DriveFuncNames>
      <PrintFuncNames ('Specializable functions:') e.SpecFuncNames>
      <DoOptTree
        s.Cycles s.OptDrive s.OptIntrinsic s.OptSpec s.OptAutoMarkup
        ((e.DriveFuncNames) e.DriveInfo-Specific)
        ((e.SpecFuncNames) e.SpecInfo-Specific)
        e.AST
      >;
}

PrintFuncNames {
  (e.Title) e.Names
    = <Log-PutLine
        e.Title
        <Map
          {
            (e.FuncName) = ' ' <DisplayName e.FuncName>
          }
          e.Names
        >
      >
}

DoOptTree {
  0 s.OptDrive s.OptIntrinsic s.OptSpec s.OptAutoMarkup
  (e.DriveInfo) (e.SpecInfo) e.AST
    = <OptTree-Spec-CleanupColdFunctions e.AST>;

  s.Cycles s.OptDrive s.OptIntrinsic s.OptSpec s.OptAutoMarkup
  (e.DriveInfo) (e.SpecInfo) e.AST
    = e.AST : e.OriginAST

    = <DoOptTree-DriveLoop
        s.Cycles s.OptDrive s.OptIntrinsic (e.DriveInfo) e.AST
      >
    : s.Cycles^ (e.DriveInfo^) e.AST^

    = <DoOptTree-SpecPass s.Cycles s.OptSpec (e.SpecInfo) e.AST>
    : s.Cycles^ (e.SpecInfo^) e.AST^

    = e.AST
    : {
        e.OriginAST = 0 e.OriginAST;
        e.AST^ = s.Cycles e.AST;
      }
    : s.Cycles^ e.AST^
    = <DoOptTree
        s.Cycles s.OptDrive s.OptIntrinsic s.OptSpec s.OptAutoMarkup
        (e.DriveInfo) (e.SpecInfo) e.AST
      >;
}

DoOptTree-DriveLoop {
  0 s.OptDrive s.OptIntrinsic (e.DriveInfo) e.AST
    = <OptTree-Drive-CleanupColdCalls e.AST> : e.AST^
    = 0 (e.DriveInfo) e.AST;

  s.Cycles s.OptDrive s.OptIntrinsic (e.DriveInfo) e.AST
    = <Log-AST ('Pass ' <Symb s.Cycles> ' (before Drive)') e.AST> : e.AST^
    = e.AST : e.OriginAST
    = <OptTree-ExpandClosures e.AST> : e.AST^
    = <Dec s.Cycles> : s.Cycles^
    = <OptTree-Drive s.OptDrive s.OptIntrinsic (e.DriveInfo) e.AST>
    : (e.DriveInfo^) e.AST^
    = e.AST
    : {
        e.OriginAST
          = s.Cycles (e.DriveInfo)
            <OptTree-Drive-CleanupColdCalls e.AST>;

        e.AST^
          = <DoOptTree-DriveLoop
              s.Cycles s.OptDrive s.OptIntrinsic (e.DriveInfo) e.AST
            >;
      };
}

DoOptTree-SpecPass {
  0 s.OptSpec (e.SpecInfo) e.AST = (e.SpecInfo) e.AST;

  s.Cycles s.OptSpec (e.SpecInfo) e.AST
    = <Log-AST ('Pass ' <Symb s.Cycles> ' (before Spec)') e.AST> : e.AST^
    = <Dec s.Cycles> <OptTree-Spec s.OptSpec (e.SpecInfo) e.AST>
}



/**
  <OptTree-CleanupMarkup e.AST> == e.OptAST
*/
$ENTRY OptTree-CleanupMarkup {
  e.AST = <OptTree-Aux 0 NoOpt NoOpt NoOpt NoOpt e.AST>;
}


OptTree-ExpandClosures {
  e.AST
    = <Map
        {
          (Function s.ScopeClass (e.Name) Sentences e.Body)
            = (Function
                s.ScopeClass (e.Name) Sentences
                <ExpandClosures-Body e.Body>
              );

          t.OtherItem = t.OtherItem;
        }
        e.AST
      >;
}

ExpandClosures-Body {
  e.Sentences = <Map &ExpandClosures-Sentence e.Sentences>;
}

ExpandClosures-Sentence {
  ((e.Pattern) e.Conditions (e.Result))
    = (
        (e.Pattern)
        <Map
          {
            (Condition (e.Name) (e.Result^) (e.Pattern^))
              = (Condition
                  (e.Name)
                  (<ExpandClosures-Result e.Result>)
                  (e.Pattern)
                );
          }
          e.Conditions
        >
        (<ExpandClosures-Result e.Result>)
      );
}

ExpandClosures-Result {
  e.Result = <Map &ExpandClusures-ResultTerm e.Result>;
}

ExpandClusures-ResultTerm {
  (Brackets e.Expr) = (Brackets <ExpandClosures-Result e.Expr>);

  (ADT-Brackets (e.Name) e.Expr)
    = (ADT-Brackets (e.Name) <ExpandClosures-Result e.Expr>);

  (CallBrackets (ClosureBrackets e.Content) e.Expr)
    = (CallBrackets <ExpandClosures-Result e.Content e.Expr>);

  (CallBrackets e.Expr) = (CallBrackets <ExpandClosures-Result e.Expr>);

  /*
    Анализировать холодные вызовы (ColdCallBrackets …) не нужно,
    поскольку они были проанализированы на предыдущих проходах
    и с тех пор не менялись.
  */

  t.OtherTerm = t.OtherTerm;
}

